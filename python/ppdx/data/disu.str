* try to autodetect disulfide bridges and patch structure
*

set dcut 2.3 ! SG atoms separated by less than this distance are assumed to be connected via a disulfide bridge
!set cmdfile "add_disulfide.str"
!system "echo '*' disulfide bridge patches > @CMDFILE"
!system "echo '*' >> @CMDFILE"

open unit 30 name add_disulfide.str write form

echu 30

echo * disulfide bridge patches
echo *

define sgs sele resname CYS .and. type SG end
if ?nsel .lt. 2 return

label disuloop1
 set s1   ?selatom
 set seg1 ?selsegi
 set res1 ?selresi
 define rest sele sgs .and. .not. bynum @{s1} end
 label disuloop2
 set s2   ?selatom
 set seg2 ?selsegi
 set res2 ?selresi
 ! compute distance
 quick sele bynum @{s1} end sele bynum @{s2} end
 !echo ?dist
 if ?dist lt @dcut then ! update patch script
  !system "echo 'patch disu @{SEG1} @{RES1} @{SEG2} @{RES2}' >> @CMDFILE "
  echo patch disu @{SEG1} @{RES1} @{SEG2} @{RES2}
  goto break
 endif
 define rest sele rest .and. .not. bynum @{s2} end
 if ?nsel ge 1 -
 goto disuloop2
 label break
 define sgs sele sgs .and. .not. ( bynum @{s1} .or. bynum @{s2} ) end
 if ?nsel ge 2 -
goto disuloop1

close unit 30

! execute the script
stream add_disulfide.str
return
